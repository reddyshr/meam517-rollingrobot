dnl Process this file with autoconf to produce a configure script.
AC_INIT(lib_src/nlp.f)

if test ! -f ../src/snopta.f; then
	echo The snadiopt directory must be located inside the
	echo snopt distribution before snadiopt can be configured and built.
	exit 1
fi

AC_CONFIG_AUX_DIR(../config)
AC_CANONICAL_HOST


dnl Checks for programs.
AC_PROG_INSTALL

case "$host_os" in

linux-gnu* )
	dnl if F77 is not set, g77 is taken as the default
;;
*)
	dnl if F77 is not set, force us to check f77 first (before g77)
	if test -z "$F77"; then
	   AC_CHECK_PROG(F77, f77, f77)
	fi
;;
esac

FFLAGS=-O
AC_PROG_F77
AC_F77_LIBRARY_LDFLAGS
AC_LANG_FORTRAN77

if test -z "$PERL"; then
	AC_PATH_PROG( PERL, perl, unknown )
fi
unset perl_ok
if test $PERL != unknown; then
	AC_MSG_CHECKING([whether \$PERL points to perl, version 5])
	if $PERL -v | grep "version 5" > /dev/null; then
		AC_MSG_RESULT(yes)
		perl_ok=1
	elif $PERL -v | grep "v5." > /dev/null; then
		AC_MSG_RESULT(yes)
		perl_ok=1
	else
		AC_MSG_RESULT(no)
		AC_MSG_WARN(SnadiOpt may not work with this version of perl.)
	fi
else
	AC_MSG_WARN(I could not find a perl distribution. Using \"perl\".)
	PERL=perl
fi

AC_MSG_CHECKING(for the Adifor program)
adifor_ok=1
if test -n "$AD_HOME"; then
	if test -x $AD_HOME/bin/Adifor; then
		AC_MSG_RESULT(found)
	else
		unset adifor_ok
		AC_MSG_RESULT(not found)
		if test -f $AD_HOME/bin/Adifor; then
			AC_MSG_WARN( [\$AD_HOME/bin/Adifor is not executable by you.] )
		else
			AC_MSG_WARN( [The AD_HOME environment appears to be incorrect.] )
		fi
	fi
else
	unset adifor_ok
	AC_MSG_RESULT(not found)
	AC_MSG_WARN( [The AD_HOME environment variable was not set.
	I have no idea where to find the Adifor program.] )
	AD_HOME='\${HOME}/ADIFOR2.0'
fi

AC_SUBST(AD_HOME)

if test -z "$AD_OS"; then
	AC_MSG_CHECKING(for a value of AD_OS)
	case "$host_os" in

	linux-gnu* )
		AD_OS=Linux86
	;;
	solaris* | sunos5* )
		AD_OS=SunOS-5.x
	;;
	sunos4* )
		AD_OS=SunOS-4.x
	;;
	aix* )
		AD_OS=AIX
	;;
	irix* )
		AD_OS=IRIX
	;;
	*)
		AD_OS=unknown
	;;
	esac
	AC_MSG_RESULT($AD_OS)
	if test "$AD_OS" = "unknown"; then
		unset adifor_ok
		warning=`echo I do not know how to set the value of \
					 AD_OS for this host.`
		AC_MSG_WARN($warning)
	fi
fi

AC_SUBST(AD_OS)

if test -z "$AD_LIB"; then
	unset adifor_ok
	AC_MSG_WARN( [The AD_LIB environment variable is not set.
	I do not know where to find the ADIFOR libraries.] )
	AD_LIB="\${HOME}/ADIFOR2.O.lib"
else
	dnl AD_LIB is set. Do AD_LIB and AD_OS combine to give a valid location
	dnl for the ADIFOR libraries

	ad_lib_ok=1
	ADIFORLIBS="${AD_LIB}/lib/ReqADIntrinsics-${AD_OS}.o \
		${AD_LIB}/lib/libADIntrinsics-${AD_OS}.a"

	AC_MSG_CHECKING(whether the ADIFOR libraries exist)
	for ad_lib in $ADIFORLIBS; do
		if test ! -f $ad_lib; then
			if test -n "$ad_lib_ok"; then
				AC_MSG_RESULT(no)
				unset ad_lib_ok
			fi
			unset adifor_ok
			warning=`echo The ADIFOR library, $ad_lib, does not exist. \
				Check the values of the AD_LIB and AD_OS \
				environment variables.`
			AC_MSG_WARN($warning)
		fi
	done
	if test -n "$ad_lib_ok"; then
		dnl The ADIFOR libs do exist
		AC_MSG_RESULT(yes)
		dnl Can we link against them?
		OLDLIBS=$LIBS
		LIBS="$LIBS $ADIFORLIBS"
		AC_MSG_CHECKING(for ehrpt in the ADIFOR libraries)
		AC_TRY_LINK_FUNC( ehrpt,
			dnl The link succeeded
			AC_MSG_RESULT(yes),
			dnl The link failed
			adifor_ok=0
			AC_MSG_RESULT(no)
			warning=`echo There seems to be some problem \
				with the ADIFOR libraries: $ADIFORLIBS`
				AC_MSG_WARN($warning)
		) # End AC_TRY_LINK_FUNC
	fi

	LIBS=$OLDLIBS
fi # end AD_LIB is set

AC_SUBST(AD_LIB)

all_snadiopt='all_snadiopt'
install_snadiopt='install_snadiopt'

if test -z "$perl_ok"; then
	AC_MSG_WARN(There was some problem with perl.)
	AC_MSG_WARN(You will not be able to install or use)
	AC_MSG_WARN(the perl support for automatic diffentiation)
	AC_MSG_WARN(until the problem is resolved.)

#   Cannot install the perl part of snadiopt
	all_snadiopt='all_snadiopt_libs'
	install_snadiopt='install_snadiopt_libs'
fi

if test -z "$adifor_ok"; then
	echo
	AC_MSG_WARN([I could not find a valid ADIFOR distribution on your system.])
	AC_MSG_WARN([If you have installed ADIFOR, then check the values of the])
	AC_MSG_WARN([AD_HOME, AD_LIB and AD_OS environment variables.])
	AC_MSG_WARN([You may continue to install SNOPT, but you will not be able to])
	AC_MSG_WARN([use the automatic differentiation support until this problem])
	AC_MSG_WARN([is resolved.])

#   Can not do anything useful without adifor. Disable everything
	unset all_snadiopt
	unset install_snadiopt
fi

AC_SUBST(all_snadiopt)
AC_SUBST(install_snadiopt)

dnl Create ../lib/snadiopt if it does not already exist
$INSTALL -d ../lib/snadiopt

AC_OUTPUT(Submakefile ../lib/snadiopt/make_defaults:make_defaults.in)

